buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        jcenter()

    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "org.spongepowered.mixin"

def modId = "${modName}".toLowerCase()
version = "${modVersion}-${mcVersion}"
group = "${mavenGroup}.${modId}"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: "snapshot", version: "${mcpMapping}-${mcpVersion}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"

            property "mixin.env.disableRefMap", "true"
            arg "-mixin.config=${modId}.mixins.json".toString()

            mods { sodium { source sourceSets.main } }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mcVersion}-${forgeVersion}"
}

mixin {
    add sourceSets.main, "${modId}.mixins.refmap.json"
}

jar.manifest.attributes([
        "Specification-Title"     : "${modId}",
        "Specification-Vendor"    : "${modId}areus",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${version}",
        "Implementation-Vendor"   : "${modId}areus",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs"            : "${modId}.mixins.json"
])

